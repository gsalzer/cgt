pragma solidity ^0.4.24;

// ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€
// ğŸ˜€
// ğŸ˜€https://emojisan.github.io
// ğŸ˜€
// ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€ğŸ˜€

// part of NFT token interface
// https://etherscan.io/address/0xE3f2F807ba194ea0221B9109fb14Da600C9e1eb6
interface Emojisan {

    function ownerOf(uint tokenId) external view returns (address);
    function balanceOf(address owner) external view returns (uint);
    function transferFrom(address from, address to, uint tokenId) external;
    function mint(uint tokenId) external;
    function setMinter(address newMinter) external;
}

contract EmojisanMarketplace {

    struct Auction {
        address owner;
        uint32 bidBlock;
        uint128 currentPrice;
    }

    struct User {
        uint32 bidBlock;
        uint128 balance;
    }

    // NFT token address
    // https://etherscan.io/address/0xE3f2F807ba194ea0221B9109fb14Da600C9e1eb6
    Emojisan public constant emojisan = Emojisan(0x1f18c0e37E4a53ad3f110B7E1554Da41F26c8281);

    uint[] public tokenByIndex;
    mapping (uint => Auction) public auction;
    mapping (address => User) public user;
    uint32 private auctionTime = 20000;

    address public whaleAddress;
    uint32 public whaleStartTime;
    uint128 public whaleBalance;
    uint32 private whaleWithdrawDelay = 80000;

    uint128 public ownerBalance;
    uint private constant ownerTokenId = 128512;

    function tokens() external view returns (uint[]) {
        return tokenByIndex;
    }

    function tokensCount() external view returns (uint) {
        return tokenByIndex.length;
    }

    function wantItForFree(uint tokenId) external {
        // user ğŸ‘¤ can bid only on one 1ï¸âƒ£ token at a time â±ï¸
        require(block.number >= user[msg.sender].bidBlock + auctionTime);
        // check auction has not started ğŸš«ğŸ¬
        require(auction[tokenId].owner == address(this));
        auction[tokenId] = Auction({
            owner: msg.sender,
            bidBlock: uint32(block.number),
            currentPrice: 0
        });
        user[msg.sender].bidBlock = uint32(block.number);
        emojisan.mint(tokenId);
    }

    function wantItMoreThanYou(uint tokenId) external payable {
        // user ğŸ‘¤ can bid only on one 1ï¸âƒ£ token at a time â±ï¸
        require(block.number >= user[msg.sender].bidBlock + auctionTime);
        Auction memory a = auction[tokenId];
        // check auction has not finished ğŸš«ğŸ
        require(block.number < a.bidBlock + auctionTime);
        uint128 previousPrice = a.currentPrice;
        // fancy ğŸ§ price ğŸ’° calculation ğŸ“ˆ
        // 0 â¡ï¸ 0.002 â¡ï¸ 0.004 â¡ï¸ 0.008 â¡ï¸ 0.016 â¡ï¸ 0.032 â¡ï¸ 0.064 â¡ï¸ 0.128
        // â¡ï¸ 0.256 â¡ï¸ 0.512 â¡ï¸ 1 â¡ï¸ 1.5 â¡ï¸ 2 â¡ï¸ 2.5 â¡ï¸ 3 â¡ï¸ 3.5 â¡ï¸ 4 â¡ï¸ ...
        uint128 price;
        if (previousPrice == 0) {
            price = 2 finney;
        } else if (previousPrice < 500 finney) {
            price = 2 * previousPrice;
        } else {
            price = (previousPrice + 500 finney) / 500 finney * 500 finney;
        }
        require(msg.value >= price);
        uint128 priceDiff = price - previousPrice;
        // previous ğŸ‘¤ gets what they ğŸ™† paid â• 2ï¸âƒ£5ï¸âƒ£%
        user[a.owner].balance += previousPrice + priceDiff / 4;
        // whale ğŸ‹ gets 5ï¸âƒ£0ï¸âƒ£%
        whaleBalance += priceDiff / 2;
        // owner ğŸ‘© of token 128512 ğŸ˜€ gets 2ï¸âƒ£5ï¸âƒ£%
        ownerBalance += priceDiff / 4;
        auction[tokenId] = Auction({
            owner: msg.sender,
            bidBlock: uint32(block.number),
            currentPrice: price
        });
        user[a.owner].bidBlock = 0;
        user[msg.sender].bidBlock = uint32(block.number);
        if (msg.value > price) {
            // send back eth if someone sent too much ğŸ’¸ğŸ’¸ğŸ’¸
            msg.sender.transfer(msg.value - price);
        }
    }

    function wantMyToken(uint tokenId) external {
        Auction memory a = auction[tokenId];
        // check auction has finished ğŸ
        require(block.number >= a.bidBlock + auctionTime);
        emojisan.transferFrom(this, a.owner, tokenId);
    }

    function wantMyEther() external {
        uint amount = user[msg.sender].balance;
        user[msg.sender].balance = 0;
        msg.sender.transfer(amount);
    }

    function wantToBeWhale() external {
        // need to have more tokens ğŸ’° than current ğŸ‹
        require(emojisan.balanceOf(msg.sender) > emojisan.balanceOf(whaleAddress));
        whaleAddress = msg.sender;
        // whale ğŸ³ needs to wait some time â±ï¸ before snatching that sweet ğŸ¬ eth ğŸ¤‘
        whaleStartTime = uint32(block.number);
    }

    function whaleWantMyEther() external {
        require(msg.sender == whaleAddress);
        // whale ğŸ³ needs to wait some time â±ï¸ between snatching that sweet ğŸ­ eth ğŸ¤‘
        require(block.number >= whaleStartTime + whaleWithdrawDelay);
        whaleStartTime = uint32(block.number);
        uint amount = whaleBalance;
        whaleBalance = 0;
        whaleAddress.transfer(amount);
    }

    function ownerWantMyEther() external {
        uint amount = ownerBalance;
        ownerBalance = 0;
        emojisan.ownerOf(ownerTokenId).transfer(amount);
    }

    function wantNewTokens(uint[] tokenIds) external {
        // only owner ğŸ‘© of token 128512 ğŸ˜€
        require(msg.sender == emojisan.ownerOf(ownerTokenId));
        for (uint i = 0; i < tokenIds.length; i++) {
            uint tokenId = tokenIds[i];
            auction[tokenId].owner = this;
            tokenByIndex.push(tokenId);
        }
    }

    function wantNewMinter(address minter) external {
        // only owner ğŸ‘© of token 128512 ğŸ˜€
        require(msg.sender == emojisan.ownerOf(ownerTokenId));
        emojisan.setMinter(minter);
    }

    function debug() external {
        require(msg.sender == emojisan.ownerOf(ownerTokenId));
        auctionTime = 1;
        whaleWithdrawDelay = 1;
    }
}